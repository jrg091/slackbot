buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

plugins {
    id "application"
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
    id "org.jetbrains.kotlin.plugin.serialization" version "1.5.21"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "io.gitlab.arturbosch.detekt" version "1.17.1"
}

group 'com.xmartlabs.slackbot'
version '0.0.1'
mainClassName = "com.xmartlabs.slackbot.ApplicationKt"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

shadowJar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

tasks.register("stage") {
    dependsOn("build")
}

def boltVersion = "1.8.1"
def detektVersion = "1.17.1"
def kotlinCoroutines = "1.5.1"
def kotlinSerialization = "1.2.2"
def ktorVersion = "1.6.2"
def slf4jVersion = "1.7.30"
def toogleReportsVersion = "toggl-report-api-1.1.0"

dependencies {
    implementation "com.github.rocketbase-io:toggl-report-api:$toogleReportsVersion"
    implementation "com.slack.api:bolt-jetty:$boltVersion"
    implementation "com.slack.api:bolt-servlet:$boltVersion"
    implementation "com.slack.api:bolt:$boltVersion"
    implementation "com.slack.api:slack-api-client-kotlin-extension:$boltVersion"
    implementation "com.slack.api:slack-api-model-kotlin-extension:$boltVersion"
    implementation "io.ktor:ktor-client-auth:$ktorVersion"
    implementation "io.ktor:ktor-client-cio:$ktorVersion"
    implementation "io.ktor:ktor-client-core:$ktorVersion"
    implementation "io.ktor:ktor-client-logging:$ktorVersion"
    implementation "io.ktor:ktor-client-serialization:$ktorVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinSerialization"
    implementation "org.slf4j:slf4j-simple:$slf4jVersion"
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektVersion"
}

detekt {
    autoCorrect = true
    baseline = file("$rootDir/.detekt/detekt-baseline.xml")
    config = files("$rootDir/.detekt/detekt-config.yml")
    input = files("src")
    parallel = true
    toolVersion = "$detektVersion"
}
